plugins {
    id 'application'
    id 'eclipse'
    id 'java'
}

group = 'com.glencoesoftware.ldaptool'
version = '0.4.1-SNAPSHOT'

mainClassName = 'com.glencoesoftware.ldaptool.Main'

compileJava {
    options.release = 11
}

repositories {
    mavenCentral()
    maven {
        name 'Unidata'
        url 'https://artifacts.unidata.ucar.edu/content/repositories/unidata-releases'
    }
    maven {
        url 'https://artifacts.openmicroscopy.org/artifactory/maven/'
    }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'minutes'
    exclude group: 'com.drewnoakes'
    exclude group: 'com.jamonapi'
    exclude group: 'com.zeroc', module: 'icestorm'
    exclude group: 'commons-beanutils'
    exclude group: 'geronimo-spec'
    exclude group: 'gnu.getopt'
    exclude group: 'net.sf.ehcache'
    exclude group: 'org.apache.pdfbox'
    exclude group: 'org.apache.xmlgraphics'
    exclude group: 'org.ini4j'
    exclude group: 'org.javassist'
    exclude group: 'org.quartz-scheduler'
    exclude group: 'org.subethamail'
    exclude group: 'xerces'
    exclude group: 'xalan'
}

dependencies {
    implementation 'org.openmicroscopy:omero-blitz:5.7.1'
    implementation 'org.springframework.security:spring-security-ldap:4.2.4.RELEASE'
    implementation 'ch.qos.logback:logback-classic:1.2.12'
    implementation 'ch.qos.logback:logback-core:1.2.12'
    implementation 'org.slf4j:jcl-over-slf4j:1.7.36'
    implementation 'info.picocli:picocli:4.7.5'
    testImplementation 'org.testng:testng:6.10'
}

jar {
    manifest {
        attributes(
            "Created-By": "Gradle ${gradle.gradleVersion}",
            "Build-Jdk": "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
            "Built-By": System.properties['user.name'],
            "Built-On": new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
            "Implementation-Build": "git rev-parse --verify HEAD".execute().getText().trim(),
            "Implementation-Title": "OMERO LDAP tool",
            "Implementation-Version": archiveVersion,
            "Implementation-Vendor": "Glencoe Software Inc.",
            "Main-Class": mainClassName,
        )
    }
}

test {
  // enable TestNG support (default is JUnit)
  useTestNG()
}

distributions {
    main {
        contents {
            from("$projectDir") {
                include 'README.md'
                include 'LICENSE.txt'
            }
        }
    }
}

